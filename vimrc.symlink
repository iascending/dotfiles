scriptencoding utf-8
source ~/dotfiles/nvim/plugins.vim

augroup myvimrc
  autocmd!
augroup END

set runtimepath+=~/.vim/plugged

" set path for :find config files
set path+=~/**1,~/.vim/**,~/dotfiles/**,~/.config/**,~/work/preprocess/**,~/Desktop/Projects/useful-commands/**

" Set encoding for editor itself, scriptencoding for vim scripts
set encoding=utf-8

" Enable mouse in all mode
set mouse=a

" Map leader key to space
let mapleader=" "

" Enable true color support
set termguicolors
let &t_8f = "\<Esc>[38:2:%lu:%lu:%lum"
let &t_8b = "\<Esc>[48:2:%lu:%lu:%lum"

" Vim airline theme
let g:airline_theme='papercolor'

" Change vertical split character to be a space (essentially hide it)
set fillchars+=vert:.

" Set preview window to appear at bottom or right
set splitbelow
set splitright

" Don't dispay mode in command line (airilne already shows it)
set noshowmode
set nocompatible

" Don't give completion message
set shortmess+=c

set hlsearch
set incsearch

set wildmenu

set ignorecase
set showmatch
set smartcase

" Set shell used to execute commands
set shell=${SHELL}
set shellcmdflag=-c

" Automatically re-read file if a change was detected outside of vim
set autoread
autocmd myvimrc CursorHold * checktime

syntax on
filetype plugin indent on

" Enable line numbers
set number

" Yank and paste with system clipboard
set clipboard=unnamed

" Do not wrap long lines by default
set nowrap

" Hide buffers instead of closing them
set hidden

" Insert spaces when TAB is pressed
set expandtab

" Show a visual vertical line under cursor
set cursorcolumn

" Show a visual horizontal line under cursor
set cursorline

" Show window title
set title

" Set foldmethold and level for folding
set foldmethod=indent
set foldlevel=99

" Set not to fold lines when opening file
set nofoldenable

" --------------- vim plugin setup ---- denite---------------------------------
" Wrap in try/catch to avoid errors on initial install before plugin is available
try
" === Denite setup ==="
" Use ripgrep for searching current directory for files
" By default, ripgrep will respect rules in .gitignore
"   --files: Print each file that would be searched (but don't search)
"   --glob:  Include or exclues files for searching that match the given glob
call denite#custom#var('file/rec', 'command', ['rg', '--files', '--glob', '!.git'])

" Use ripgrep in place of grep
call denite#custom#var('grep', 'command', ['rg', '--glob', '!.git'])

" Custom options for ripgrep
"   --vimgrep:  Show results with every match on it's own line
"   --hidden:   Search hidden directories and files
"   --heading:  Show the file name above clusters of matches from each file
"   --S:        Search case insensitively if the pattern is all lowercase
call denite#custom#var('grep', 'default_opts', ['--hidden', '--vimgrep', '--heading', '-S', '-F'])
" call denite#custom#var('grep', 'default_opts', ['--hidden', '--vimgrep', '--heading', '-S'])

" Recommended defaults for ripgrep via Denite docs
call denite#custom#var('grep', 'recursive_opts', [])
call denite#custom#var('grep', 'pattern_opt', ['--regexp'])
call denite#custom#var('grep', 'separator', ['--'])
call denite#custom#var('grep', 'final_opts', [])

" Remove date from buffer list
call denite#custom#var('buffer', 'date_format', '')

" Custom options for Denite
"   auto_resize             - Auto resize the Denite window height automatically.
"   prompt                  - Customize denite prompt
"   direction               - Specify Denite window direction as directly below current pane
"   winminheight            - Specify min height for Denite window
"   highlight_mode_insert   - Specify h1-CursorLine in insert mode
"   prompt_highlight        - Specify color of prompt
"   highlight_matched_char  - Matched characters highlight
"   highlight_matched_range - matched range highlight
let s:denite_options = {'default' : {
  \ 'split': 'floating',
  \ 'start_filter': 1,
  \ 'auto_resize': 1,
  \ 'source_names': 'short',
  \ 'prompt': 'λ ',
  \ 'highlight_matched_char': 'QuickFixLine',
  \ 'highlight_matched_range': 'Visual',
  \ 'highlight_window_background': 'Visual',
  \ 'highlight_filter_background': 'DiffAdd',
  \ 'winrow': 1,
  \ 'preview_width': (&columns - 40) * 0.50,
  \ 'vertical_preview': 1
  \ }}

" Loop through denite options and enable them
function! s:profile(opts) abort
  for l:fname in keys(a:opts)
    for l:dopt in keys(a:opts[l:fname])
      call denite#custom#option(l:fname, l:dopt, a:opts[l:fname][l:dopt])
    endfor
  endfor
endfunction

call s:profile(s:denite_options)
catch
  echo 'Denite not installed. It should work after running :PlugInstall'
endtry

" --------------- vim plugin setup ---- denite keymappings --------------------
" Define mappings while in 'filter' mode
"   <C-o>         - Switch to normal mode inside of search results
"   <Esc>         - Exit denite window in any mode
"   <CR>          - Open currently selected file in any mode
"   <C-t>         - Open currently selected file in a new tab
"   <C-v>         - Open currently selected file a vertical split
"   <C-h>         - Open currently selected file in a horizontal split
autocmd myvimrc FileType denite-filter call s:denite_filter_my_settings()
function! s:denite_filter_my_settings() abort
  imap <silent><buffer> <C-o>
  \ <Plug>(denite_filter_update)
  inoremap <silent><buffer><expr> <Esc>
  \ denite#do_map('quit')
  nnoremap <silent><buffer><expr> <Esc>
  \ denite#do_map('quit')
  inoremap <silent><buffer><expr> <CR>
  \ denite#do_map('do_action')
  inoremap <silent><buffer><expr> <C-t>
  \ denite#do_map('do_action', 'tabopen')
  inoremap <silent><buffer><expr> <C-v>
  \ denite#do_map('do_action', 'vsplit')
  inoremap <silent><buffer><expr> <C-h>
  \ denite#do_map('do_action', 'split')
endfunction

" --------------- vim plugin setup ---- denite keymappings in default mode ----
" Define mappings indow
"   <CR>        - Opens currently selected file
"   q or <Esc>  - Quit Denite window
"   d           - Delete currenly selected file
"   p           - Preview currently selected file
"   <C-o> or i  - Switch to insert mode inside of filter prompt
"   <C-t>       - Open currently selected file in a new tab
"   <C-v>       - Open currently selected file a vertical split
"   <C-h>       - Open currently selected file in a horizontal split
autocmd myvimrc FileType denite call s:denite_my_settings()
function! s:denite_my_settings() abort
  nnoremap <silent><buffer><expr> <CR>
  \ denite#do_map('do_action')
  nnoremap <silent><buffer><expr> q
  \ denite#do_map('quit')
  nnoremap <silent><buffer><expr> <Esc>
  \ denite#do_map('quit')
  nnoremap <silent><buffer><expr> d
  \ denite#do_map('do_action', 'delete')
  nnoremap <silent><buffer><expr> p
  \ denite#do_map('do_action', 'preview')
  nnoremap <silent><buffer><expr> i
  \ denite#do_map('open_filter_buffer')
  nnoremap <silent><buffer><expr> <C-t>
  \ denite#do_map('do_action', 'tabopen')
  nnoremap <silent><buffer><expr> <C-v>
  \ denite#do_map('do_action', 'vsplit')
  nnoremap <silent><buffer><expr> <C-h>
  \ denite#do_map('do_action', 'split')
endfunction

" --------------- vim plugin setup ---- coc.nvim ------------------------------
"  disable coc.nvim for git buffer, which causes 'git log | vim -R -' slow
let g:no_coc_file_types = ['git']
function! s:disable_coc_for_type()
  if index(g:no_coc_file_types, &filetype) != -1
    let b:coc_enabled = 0
  endif
endfunction
augroup CocGroup
  autocmd!
  autocmd BufNew,BufEnter * call s:disable_coc_for_type()
augroup end

" use <tab> for trigger completion and navigate to next complete item
function! s:check_back_space() abort
  let col = col('.') - 1
  return !col || getline('.')[col - 1]  =~# '\s'
endfunction

inoremap <silent><expr> <TAB>
  \ pumvisible() ? "\<C-n>" :
  \ <SID>check_back_space() ? "\<TAB>" :
  \ coc#refresh()
inoremap <expr><S-TAB> pumvisible() ? "\<C-p>" : "\<C-h>"

" Use <c-space> to trigger completion.
if has('nvim')
  inoremap <silent><expr> <c-space> coc#refresh()
else
  inoremap <silent><expr> <c-@> coc#refresh()
endif

" Make <CR> auto-select the first completion item and notify coc.nvim to
" format on enter, <cr> could be remapped by other vim plugin
inoremap <silent><expr> <cr> pumvisible() ? coc#_select_confirm()
  \: "\<C-g>u\<CR>\<c-r>=coc#on_enter()\<CR>"

" Use `[g` and `]g` to navigate diagnostics
" Use :CocDiagnostics` to get all diagnostics of current buffer in location list.
nmap <nowait><silent>[g <Plug>(coc-diagnostic-prev)
nmap <nowait><silent>]g <Plug>(coc-diagnostic-next)
nmap <nowait><silent>rn <Plug>(coc-rename)
xmap <nowait><silent>cf <Plug>(coc-format-selected)
nmap <nowait><silent>cf <Plug>(coc-format-selected)
nnoremap <nowait><silent>cl :CocDiagnostics<cr>

" Use K to show documentation in preview window.
function! s:show_documentation()
  if (index(['vim','help'], &filetype) >= 0)
    execute 'h '.expand('<cword>')
  elseif (coc#rpc#ready())
    call CocActionAsync('doHover')
  else
    execute '!' . &keywordprg . " " . expand('<cword>')
  endif
endfunction
autocmd myvimrc FileType javascript,vue,python nnoremap <buffer><nowait><silent>K :call <SID>show_documentation()<CR>

" Highlight the symbol and its references when holding the cursor.
autocmd myvimrc CursorHold * silent call CocActionAsync('highlight')

"Close preview window when completion is done.
autocmd! myvimrc CompleteDone * if pumvisible() == 0 | pclose | endif

" --------------- vim plugin setup ---- NeoSnippet ----------------------------
" Map <C-k> as shortcut to activate snippet if available
imap <C-k> <Plug>(neosnippet_expand_or_jump)
smap <C-k> <Plug>(neosnippet_expand_or_jump)
xmap <C-k> <Plug>(neosnippet_expand_target)

" Load custom snippets from snippets folder
let g:neosnippet#snippets_directory='~/.config/nvim/snippets'

" Hide conceal markers
let g:neosnippet#enable_conceal_markers = 0

" --------------- vim plugin setup ---- NerdTree ------------------------------
" Show hidden files/directories
let g:NERDTreeShowHidden = 1
" Remove bookmarks and help text from NERDTree
let g:NERDTreeMinimalUI = 1
" Set NERDTree default window size
let g:NERDTreeWinSize=60

let g:webdevicons_enable = 1
let g:webdevicons_enable_nerdtree = 1
let g:webdevicons_enable_unite = 1
let g:webdevicons_enable_vimfiler = 1
let g:webdevicons_enable_airline_tabline = 1
let g:webdevicons_enable_airline_statusline = 1
let g:webdevicons_enable_ctrlp = 1
let g:webdevicons_enable_flagship_statusline = 1
let g:WebDevIconsUnicodeDecorateFileNodes = 1
let g:WebDevIconsUnicodeGlyphDoubleWidth = 1
let g:webdevicons_conceal_nerdtree_brackets = 1
let g:WebDevIconsNerdTreeAfterGlyphPadding = '  '
let g:WebDevIconsNerdTreeGitPluginForceVAlign = 1
let g:webdevicons_enable_denite = 1
let g:WebDevIconsUnicodeDecorateFolderNodes = 1
let g:DevIconsEnableFoldersOpenClose = 1
let g:DevIconsEnableFolderPatternMatching = 1
let g:DevIconsEnableFolderExtensionPatternMatching = 1
let WebDevIconsUnicodeDecorateFolderNodesExactMatches = 1
" Custom icons for expandable/expanded directories
let g:NERDTreeDirArrowExpandable = '▸'
let g:NERDTreeDirArrowCollapsible = '▾'
let g:WebDevIconsNerdTreeBeforeGlyphPadding = ''

" Hide certain files and directories from NERDTree
let g:NERDTreeIgnore = ['^\.DS_Store$', '^tags$', '\.git$[[dir]]', '\.idea$[[dir]]', '\.sass-cache$']

" --------------- vim plugin setup ---- vim airline ---------------------------
 " Wrap in try/catch to avoid errors on initial install before plugin is available
try
" === Vim airline ==== "
" Enable extensions
let g:airline_extensions = ['branch', 'hunks', 'coc']

" Do not draw separators for empty sections (only for the active window) >
let g:airline_skip_empty_sections = 1

" Smartly uniquify buffers names with similar filename, suppressing common parts of paths.
let g:airline#extensions#tabline#formatter = 'unique_tail'

" Custom setup that removes filetype/whitespace from default vim airline bar
let g:airline#extensions#default#layout = [['a', 'b', 'c'], ['x', 'z', 'warning', 'error']]

" Customize vim airline per filetype
" 'nerdtree'  - Hide nerdtree status line
" 'list'      - Only show file type plus current line number out of total
let g:airline_filetype_overrides = {
  \ 'nerdtree': [ get(g:, 'NERDTreeStatusline', ''), '' ],
  \ 'list': [ '%y', '%l/%L'],
  \ }

" Enable powerline fonts
let g:airline_powerline_fonts = 1

" Define custom airline symbols
if !exists('g:airline_symbols')
  let g:airline_symbols = {}
endif
let g:airline_symbols.paste = 'PASTE - '
let g:airline_left_sep = ''
let g:airline_left_alt_sep = ''
let g:airline_right_sep = ''
let g:airline_right_alt_sep = ''
let g:airline_symbols.branch = ''
let g:airline_symbols.readonly = ''
let g:airline_symbols.linenr = ''
let g:airline_symbols.maxlinenr = ''
let g:airline_symbols.dirty='⚡'

let g:airline_section_a = airline#section#create(['paste', 'mode'])
function! AirlineInit()
  let g:airline_section_b = airline#section#create(['branch'])
  call airline#update_statusline()
endfunction
autocmd myvimrc VimEnter * call AirlineInit()

" Enable caching of syntax highlighting groups
let g:airline_highlighting_cache = 1

" Don't show git changes to current file in airline
let g:airline#extensions#hunks#enabled=0

catch
  echo 'Airline not installed. It should work after running :PlugInPtall'
endtry

" --------------- vim plugin setup ---- echodoc -------------------------------
" Enable echodoc on startup
set cmdheight=2
let g:echodoc#enable_at_startup = 1

" --------------- vim plugin setup ---- vim-javascript ------------------------
" Enable syntax highlighting for JSDoc
let g:javascript_plugin_jsdoc = 1

"-- ------------- vim plugin setup ---- vim-jsx -------------------------------
" Highlight jsx syntax even in non .jsx files
let g:jsx_ext_required = 0

" --------------- vim plugin setup ---- javascript-libraries-syntax -----------
let g:used_javascript_libs = 'underscore,requirejs,chai,jquery'

" --------------- vim plugin setup ---- vim-signify ---------------------------
let g:signify_sign_delete = '-'

" --------------- vim plugin setup ---- vim-vue-plugin ------------------------
"  this set of config: vim-vue-plugin without vim-context-commentstring
let b:current_loading_main_syntax = 'vue'
let g:vim_vue_plugin_config = {
  \'syntax': {
  \   'template': ['html'],
  \   'script': ['javascript'],
  \   'style': ['css'],
  \},
  \'full_syntax': [],
  \'initial_indent': ['script', 'style'],
  \'attribute': 0,
  \'keyword': 0,
  \'foldexpr': 0,
  \'debug': 0,
  \}

" config vue comment format for vim-commentary plugin
function! OnChangeVueSyntax(syntax)
  echom 'Syntax is '.a:syntax
  if a:syntax == 'html'
    setlocal commentstring=<!--%s-->
    setlocal comments=s:<!--,m:\ \ \ \ ,e:-->
  elseif a:syntax =~ 'css'
    setlocal comments=s1:/*,mb:*,ex:*/ commentstring&
  else
    setlocal commentstring=//%s
    setlocal comments=sO:*\ -,mO:*\ \ ,exO:*/,s1:/*,mb:*,ex:*/,://
  endif
endfunction

" --------------- vim plugin setup ---- vim-indentline ------------------------
let g:indentLine_enabled = 1
" let g:indentLine_char = '▏'
let g:indentLine_char_list = ['|', '¦', '┆', '┊']
nnoremap <nowait><silent>yoi :IndentLinesToggle<CR>

" --------------- vim custom colorscheme setup --------------------------------
" Add custom highlights in method that is executed every time a colorscheme is sourced
" See https://gist.github.com/romainl/379904f91fa40533175dfaec4c833f2f for details
function! TrailingSpaceHighlights() abort
  " Hightlight trailing whitespace
  highlight Trail ctermbg=red guibg=red
  call matchadd('Trail', '\s\+$', 100)
endfunction

function! s:custom_jason_colors()
  " coc.nvim color changes
  hi link CocErrorSign WarningMsg
  hi link CocWarningSign Number
  hi link CocInfoSign Type

  " Make background transparent for many things
  hi Normal ctermbg=NONE guibg=NONE
  hi NonText ctermbg=NONE guibg=NONE
  hi LineNr ctermfg=NONE guibg=NONE
  hi SignColumn ctermfg=NONE guibg=NONE
  hi StatusLine guifg=#16252b guibg=#6699CC
  hi StatusLineNC guifg=#16252b guibg=#16252b

  " Try to hide vertical spit and end of buffer symbol
  hi VertSplit gui=NONE guifg=#17252c guibg=#17252c
  hi EndOfBuffer ctermbg=NONE ctermfg=NONE guibg=#17252c guifg=#17252c

  " Customize NERDTree directory
  hi NERDTreeCWD guifg=#99c794

  " Make background color transparent for git changes
  hi SignifySignAdd ctermbg=NONE guibg=NONE
  hi SignifySignDelete ctermbg=NONE guibg=NONE
  hi SignifySignChange ctermbg=NONE guibg=NONE

  " Highlight git change signs
  hi SignifySignAdd guifg=#99c794
  hi SignifySignDelete guifg=#ec5f67
  hi SignifySignChange guifg=#c594c5

  " Highlight git diff added and removed lines
  hi DiffAdded   term=reverse ctermfg=0 ctermbg=121 guifg=bg guibg=#99c794
  hi DiffRemoved term=NONE ctermfg=253 ctermbg=203 guifg=#d8dee9 guibg=#ec5f67
  hi CocUnusedHighlight ctermfg=242 guifg=#777777
endfunction

autocmd! myvimrc ColorScheme * call TrailingSpaceHighlights()
autocmd! myvimrc ColorScheme * call s:custom_jason_colors()
autocmd! myvimrc ColorScheme OceanicNext call s:custom_jason_colors()

" Call method on window enter
augroup WindowManagement
  autocmd!
  autocmd WinEnter * call Handle_Win_Enter()
augroup END

" Change highlight group of preview window when open
function! Handle_Win_Enter()
  if &previewwindow
"    setlocal winhighlight=Normal:MarkdownError
  endif
endfunction

" Editor theme
set background=dark
try
  colorscheme OceanicNext
catch
  colorscheme slate
endtry

" --------------- vim misc keymappings ----------------------------------------
" Map a ESC key to jk
inoremap jk <ESC>
inoremap kj <ESC>

" Map line start and line end
map L $
map H ^

" Do not move cursor when highlight
nnoremap * *N
nnoremap # #N

" Move to next editor link
nnoremap j gj
nnoremap k gk

nnoremap / /\v
vnoremap / /\v

" --------------- vim plugin setup ---- NerdTree ------------------------------
" Toggle NERDTree on/off
nmap <nowait><silent>yon :NERDTreeToggle<CR>

" Opens current file location in NERDTree
nmap <leader>f :NERDTreeFind<CR>

" Automaticaly close nvim if NERDTree is only thing left open
autocmd myvimrc BufEnter * if tabpagenr('$') == 1 && winnr('$') == 1 && exists('b:NERDTree') && b:NERDTree.isTabTree() |
  \ quit | endif

" If another buffer tries to replace NERDTree, put it in the other window, and bring back NERDTree.
autocmd myvimrc BufEnter * if bufname('#') =~ 'NERD_tree_\d\+' && bufname('%') !~ 'NERD_tree_\d\+' && winnr('$') > 1 |
  \ let buf=bufnr() | buffer# | execute "normal! \<C-W>w" | execute 'buffer'.buf | endif

" --------------- vim plugin setup ---- denite keymappings --------------------
" Browser currently open buffers with (Denite)
nmap <leader>; :Denite buffer<CR>

" Browse list of files in current directory (Denite)
nmap <leader>t :DeniteProjectDir file/rec<CR>

" Search current directory for occurences of given term and close window if no results
nnoremap <leader>a :<C-u>Denite grep:. -no-empty<CR>

" Search current directory for occurences of word under cursor
nnoremap <leader>r :<C-u>DeniteCursorWord grep:.<CR>

" Resume previous denite search list
nnoremap <leader>p :<C-u>Denite -resume<CR>

" --------------- vim plugin setup ---- coc.nvim keymappings ------------------
let g:coc_enable_locationlist = 0
autocmd myvimrc User CocLocationsChange CocList --no-quit --normal location

" Jump to definition of current symbol
nmap <nowait><silent>gd <Plug>(coc-definition)
" Jump to references of current symbol
nmap <nowait><silent>gr <Plug>(coc-references)
" Jump to implementation of current symbol
nmap <nowait><silent>gi <Plug>(coc-implementation)
" Auto fix current
nmap <nowait><silent>qf <Plug>(coc-fix-current)
nmap <nowait><silent>ac <Plug>(coc-codeaction)
" xmap <silent> acs <Plug>(coc-codeaction-selected)
" nmap <silent> acs <Plug>(coc-codeaction-selected)

" --------------- vim plugin setup ---- vim-better-whitespace keymappings -----
" Automatically remove trailing whitespace
nnoremap <leader>cw :StripWhitespace<CR>

" --------------- vim plugin setup ---- vim-easymotion ------------------------
" Easy-motion highlights first word letters bi-directionally
" nmap <leader>w <Plug>(easymotion-overwin-w)
nmap f <Plug>(easymotion-overwin-f2)

" --------------- vim plugin setup ---- vim-fugitive --------------------------
" :Gedit :0                    # to open index version of current file
" :Gedit another_branch_name:% # read current file of another branch
" :Ggrep "findme" branch_name  # search word on a branch
" :Ggrep "findme" commit_hash  # search word on a commit
" :Gclog --grep=findme --      # commit messages contains word
" :Gclog -S "findme" --           # commits add or remove a word
nnoremap <nowait><silent>gb :Git blame<CR>
nnoremap <nowait><silent>gB :GBrowse<CR>
vnoremap <nowait><silent>gB :GBrowse<CR>
nnoremap <nowait><silent><leader>gp :Git push<CR>
" 1) gs enter fugitive git status buffer
" 2) C-n or C-p to navigate between file names
" 3) f followed by to characters to go to any place
" 4) <CR> on a file nama to open that file
" 5) dd/dv on a file name to enter diff mode
" 6) diffget, diffput, diffupdate
" 7) dq to quit diff mode(focus in status split)
" 8) gq to quit status window
nnoremap <nowait><silent>gs :Git<CR>
" 1) gl to view git change history of current buffer
" 2) ]q or [q navigate between commits in quickfix list
" 3) dd to enter diff mode, and dq to quit diff mode
" 4) ]q or [q navigate to a different commit
" 5) dd/dv to enter diff mode, and dq to quit diff mode
" 6) yoq to toggle open or close quickfix list
" 7) Gedit to bring back to working version
nnoremap <nowait><silent>gl :0Gclog<CR>
vnoremap <nowait><silent>gl :Gclog<CR>

" " Close any corresponding diff buffer
" function! MyCloseDiff()
"   if (winnr('$') < 2)
"     return
"   endif

"   if (bufname(winbufnr(1)) !~ '^fugitive:' || bufname(winbufnr(2)) !~ '^fugitive:')
"     echom "Not in diff view."
"     return
"   endif
"   exe 'bdelete' winbufnr(1)
"   exe 'Gedit'
" endfunction
" nnoremap <nowait><silent>qd :call MyCloseDiff()<CR>

" Toggle quickfix list open and close
function! ToggleQuickFix()
  if empty(filter(getwininfo(), 'v:val.quickfix'))
    copen 25
  else
    cclose
  endif
endfunction
nnoremap <nowait><silent>yoq :call ToggleQuickFix()<CR>
autocmd BufReadPost fugitive://* set bufhidden=delete

" --------------- vim plugin setup ---- open-browser.vim  ---------------------
let g:netrw_nogx = 1
nmap gx <Plug>(openbrowser-smart-search)
vmap gx <Plug>(openbrowser-smart-search)

" --------------- vim plugin setup ---- open-browser-github.vim ---------------
nnoremap ght :OpenGithubIssue<Space>
nnoremap ghp :OpenGithubPullReq<CR>
nnoremap ghf :'<,'>OpenGithubFile<CR>
nnoremap ghm :VimuxPromptCommand<CR>ghm<CR>
nnoremap ghc viW"ay:OpenGithubCommit <C-R>a &<CR><CR>

" --------------- vim plugin setup ---- vim-signify ---------------------------
" Set updatetime for vim-signify
set updatetime=100
nnoremap <nowait><silent>du :SignifyHunkUndo<CR>
nnoremap <nowait><silent>dc :SignifyHunkDiff<CR>
nmap ]c <plug>(signify-next-hunk)
nmap [c <plug>(signify-prev-hunk)

" --------------- vim plugin setup ---- vim-test --------------------------
let test#strategy = 'vimux'
let test#python#runner = 'pytest'
let test#project_root = '/Users/liqiang/work/blendopt/app/testing'
nmap <leader>tc :TestNearest<CR>
nmap <leader>tf :TestFile<CR>
nmap <leader>tp :TestLast<CR>
nmap <leader>tv :TestVisit<CR>

" --------------- vim plugin setup ---- vimux --------------------------------
"  ---------------------------------
" run shell command from within vim
map <leader>vp :VimuxPromptCommand<CR>
" run last command again
map <leader>vl :VimuxRunLastCommand<CR>
" close vimux pane opened
map <leader>vq :VimuxCloseRunner<CR>

" --------------- vim plugin setup ---- vim-easy-align keymappings ------------
" vipga=               # visual select inner paragraph and align around
" gaip=                # align around for inner paragraph
" -=                   # align around last occurence
" 2=                   # align around 2nd occurence
" *=                   # align around every occurence
" <enter>              # left/right/center alignment modes
" gaip<enter><enter>*= # center align around every occurences
xmap ga <Plug>(EasyAlign)
nmap ga <Plug>(EasyAlign)

" --------------- vim plugin setup ---- vim-visual-star-search ----------------
function! s:VSetSearch(cmdtype)
  let temp = @s
  norm! gv"sy
  let @/ = '\V' . substitute(escape(@s, a:cmdtype.'\'), '\n', '\\n', 'g')
  let @s = temp
endfunction
xnoremap * :<C-u>call <SID>VSetSearch('/')<CR>/<C-R>=@/<CR><CR>N
xnoremap # :<C-u>call <SID>VSetSearch('?')<CR>?<C-R>=@/<CR><CR>N

" --------------- vim plugin setup ---- vim-dadbod ----------------------------
let g:db = 'postgres://paradynuser:paradynuser@127.0.0.1/' . $DB_NAME . '<CR>'
func! DBExe(...)
  if !a:0
    let &operatorfunc = matchstr(expand('<sfile>'), '[^. ]*$')
    return 'g@'
  endif
  let sel_save = &selection
  let &selection = "inclusive"
  let reg_save = @@
  if a:1 == 'char'	" Invoked from Visual mode, use gv command.
    silent exe 'normal! gvy'
  elseif a:1 == 'line'
    silent exe "normal! '[V']y"
  else
    silent exe 'normal! `[v`]y'
  endif
  execute "DB! " . @@
  let &selection = sel_save
  let @@ = reg_save
endfunc
xnoremap <expr> <Plug>(DBExe)     DBExe()
nnoremap <expr> <Plug>(DBExe)     DBExe()
nnoremap <expr> <Plug>(DBExeLine) DBExe() . '_'
xmap <leader>db  <Plug>(DBExe)
nmap <leader>db  <Plug>(DBExe)
omap <leader>db  <Plug>(DBExe)
nmap <leader>dbb <Plug>(DBExeLine)

" Disable backups and swap files
set nobackup
set nowritebackup
set noswapfile

" Reload icons after init source
if exists('g:loaded_webdevicons')
  call webdevicons#refresh()
endif

" Source vimrc
nnoremap <leader>sv :source ~/.vimrc<CR>:echo "Reloaded .vimrc"<CR>

" cd into current buffer's directory
nnoremap <leader>cd :lcd %:p:h<CR>

" Copy the full path of current buffer
nnoremap <silent> <leader>cp :let @+=expand('%:p')<CR>

" Disable vim-tmux-navigator's default key mapping
let g:tmux_navigator_no_mappings = 1
" Write all buffers before navigating from Vim to tmux pane
let g:tmux_navigator_save_on_switch = 1
" Disable tmux navigator when zooming the Vim pane
let g:tmux_navigator_disable_when_zoomed = 1
nnoremap <silent> <home>h :TmuxNavigateLeft<cr>
nnoremap <silent> <home>j :TmuxNavigateDown<cr>
nnoremap <silent> <home>k :TmuxNavigateUp<cr>
nnoremap <silent> <home>l :TmuxNavigateRight<cr>

" toggle window size
function! ToggleZoom(zoom)
  if exists("t:restore_zoom") && (a:zoom == v:true || t:restore_zoom.win != winnr())
    exec t:restore_zoom.cmd
    unlet t:restore_zoom
  elseif a:zoom
    let t:restore_zoom = { 'win': winnr(), 'cmd': winrestcmd() }
    exec "normal \<C-W>\|\<C-W>_"
  endif
endfunction
augroup restorezoom
  autocmd WinEnter * silent! :call ToggleZoom(v:false)
augroup END
nnoremap <silent><leader>g :call ToggleZoom(v:true)<CR>

" close all windows except current one
nnoremap <silent> <leader>wo :wincmd o<CR>
" close current window
nnoremap <silent> <leader>wc :wincmd c<CR>
" normalize split size
nnoremap <silent> <leader>w= :wincmd =<CR>
" Switch between horizontal and vertical split windows
nnoremap <silent> <leader>wh :windo wincmd K<CR>
nnoremap <silent> <leader>wv :windo wincmd H<CR>

" Dash API docs search shortcuts
let g:dash_activate = 1
nmap <silent><leader>ds <Plug>DashSearch

" config gf command for vue file search
" let proj_client_src = $PROJ_HOME.'/app/client/src/'
" let proj_styles_src = $PROJ_HOME.'/app/client/src/assets/styles/'
let proj_client_src = 'app/client/src/'
let proj_styles_src = 'app/client/src/assets/styles/'
set isfname+=@-@
set includeexpr=substitute(substitute(v:fname,'^@\/',proj_client_src,''),'^\\~styles\/',proj_styles_src,'')
set suffixesadd=.js,.vue,.scss

" Toggle - inclusion in keyword
nnoremap <expr> <leader>t- ':set isk' . (index(split(&isk, ','), '-') == -1 ? '+' : '-') . '=-<cr>:set iskeyword?<cr>'
nnoremap <expr> <leader>t/ ':set isk' . (index(split(&isk, ','), '/') == -1 ? '+' : '-') . '=/<cr>:set iskeyword?<cr>'

" Toggle - open current buffer in binary mode
let g:binary_mode=0
function! ToggleBinaryMode()
  silent !clear
  if !g:binary_mode
    execute "%!xxd"
  else
    execute "%!xxd -r"
  endif
  let g:binary_mode = !g:binary_mode
endfunction
nnoremap <silent> <leader>b :call ToggleBinaryMode()<CR>

" key mappings to replace vim-close behaviors
inoremap " ""<left>
inoremap ' ''<left>
inoremap ( ()<left>
inoremap (; ();<left>
inoremap (<CR> (<CR>)<ESC>O
inoremap [ []<left>
inoremap [<CR> [<CR>]<ESC>O
inoremap { {}<left>
inoremap {<CR> {<CR>}<ESC>O
inoremap {,<CR> {<CR>},<ESC>O
inoremap {;<CR> {<CR>};<ESC>O
imap cll console.log(;<Esc>==f(a

let g:coc_global_extensions = [
  \ "coc-html",
  \ "coc-css",
  \ "coc-tsserver",
  \ "coc-json",
  \ "coc-snippets",
  \ "coc-ultisnips",
  \ "coc-highlight",
  \ "coc-pyright",
  \ "coc-vetur",
  \ "coc-prettier"]

autocmd myvimrc FileType python let b:coc_root_patterns = ['.git', '.env']

" highlight the part of line longer than 80 characters
augroup TooLong
  autocmd!
  autocmd winEnter,BufEnter * call clearmatches() | call matchadd('ColorColumn', '\%>80v', 100)
augroup END

" edit all buffers and come back without losing highlight, borrowed from rominl
function! EditAllBuffersAndComeBackWithoutLosingHighlighting()
  let this_buffer = bufnr("%")
  bufdo set eventignore= | if &buftype != "nofile" && expand("%") != '' | edit | endif
  execute "b" . this_buffer
endfunction

" Redirect the vim output to a scratch buffer
function! Redir(cmd, rng, start, end)
  for win in range(1, winnr('$'))
    if getwinvar(win, 'scratch')
      execute win . 'windo close'
    endif
  endfor
  if a:cmd =~ '^!'
    let cmd = a:cmd =~' %'
      \ ? matchstr(substitute(a:cmd, ' %', ' ' . expand('%:p'), ''), '^!\zs.*')
      \ : matchstr(a:cmd, '^!\zs.*')
    if a:rng == 0
      let output = systemlist(cmd)
    else
      let joined_lines = join(getline(a:start, a:end), '\n')
      let cleaned_lines = substitute(shellescape(joined_lines), "'\\\\''", "\\\\'", 'g')
      let output = systemlist(cmd . " <<< $" . cleaned_lines)
    endif
  else
    redir => output
    execute a:cmd
    redir END
    let output = split(output, "\n")
  endif
  vnew
  let w:scratch = 1
  setlocal buftype=nofile bufhidden=wipe nobuflisted noswapfile
  call setline(1, output)
endfunction
command! -nargs=1 -complete=command -bar -range Redir silent call Redir(<q-args>, <range>, <line1>, <line2>)

nnoremap zz :w<CR>
inoremap zz <Esc>:w<CR>l
vnoremap zz <Esc>:w<CR>

" find out highlight group under cursor
function! SynStack ()
  for i1 in synstack(line("."), col("."))
    let i2 = synIDtrans(i1)
    let n1 = synIDattr(i1, "name")
    let n2 = synIDattr(i2, "name")
    echo n1 "->" n2
  endfor
endfunction
map gmm :call SynStack()<CR>

" --------------- vim plugin setup ---- vim-markdown --------------------------
let g:markdown_fenced_languages = ['html', 'python', 'bash=sh', 'sql']
let g:sql_type_default = 'pgsql'
let g:sql_type_override='pgsql'

" Search and replace
" map <leader>s :%s///<left><left>
" search word under cursor, search string in .py, .js .vue files
command! -nargs=+ Ggrw execute 'silent Ggrep!' <q-args> | cw | redraw!
command! -nargs=1 Ggjv silent Ggrep! <args> *.js *.vue | cw | redraw!
command! -nargs=1 Ggpy silent Ggrep! <args> -- "*.py" | cw | redraw!
nnoremap ss :Ggrw <cword><CR>
nnoremap sp :Ggpy<space>
nnoremap sv :Ggjv<space>

" --------------- vim-unimpaired shortcuts --------------------------
" [b ]b # :bprevious :bnext
" [q ]q # :cprevious :cnext
" [n ]n # navigate between conflict marker
" [e ]e # move current line up or down
" =p =P # paste after/before the line, reindent
" <p <P # paste after/before the line, decrease indent
" >p >P # paste after/before the line, increase indent
" yoh   # toggle highlight
" yor   # toggle relative line number
" yoi   # toggle indent lines
" yon   # toggle NerdTree
" yoq   # toggle open close quickfix list
" yow   # toggle line wrap

" --------------- vim-exchange shortcuts --------------------------
"  cxiw . # exchange 2 words
"  X      # exchange for visual mode
"  cxc    # clear pending exchange
