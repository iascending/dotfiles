# If you come from bash you might have to change your $PATH.
source ~/dotfiles/profile

# There was an long time outstanding issue with my tmux-vim-zsh
# oh-my-zsh setup. The issue was that the column number displayed
# in vim-airline statusline was incorrect. Sometimes ":" before
# column number was missing, and sometimes one extra digit was
# appended to correct column number. This issue could be fixed
# by changing the line
# alias tmux=_zsh_tmux_plugin_run
# in ~/.oh-my-zsh/plugins/tmux/tmux.plugin.zsh
# into the following line
# alias tmux=_zsh_tmux_plugin_run -2

# Would you like to use another custom folder than $ZSH/custom?
ZSH_CUSTOM=${HOME}/dotfiles/custom

# Path to oh-my-zsh installation.
export ZSH=~/.oh-my-zsh

# Name of the theme to load.
ZSH_THEME="spaceship"
SPACESHIP_PACKAGE_SHOW=false
SPACESHIP_NODE_SHOW=false
SPACESHIP_GIT_STATUS_STASHED=''

# TMUX
# Automatically start tmux
ZSH_TMUX_AUTOSTART=true

# Automatically connect to a previous session if it exists
ZSH_TMUX_AUTOCONNECT=true

# Enable command auto-correction.
ENABLE_CORRECTION="true"

# Set zsh completion cache file location
if [ -z "$ZSH_COMPDUMP" ]; then
  mkdir -p ${HOME}/.cache/zsh
  ZSH_COMPDUMP="${HOME}/.cache/zsh/.zcompdump-${SHORT_HOST}-${ZSH_VERSION}"
fi

# Display red dots whilst waiting for completion.
COMPLETION_WAITING_DOTS="true"

# Disable marking untracked files
# under VCS as dirty. This makes repository status check for large repositories
# much, much faster.
DISABLE_UNTRACKED_FILES_DIRTY="true"

# zsh-vi-mode plugin configuration
ZVM_VI_ESCAPE_BINDKEY=jk
ZVM_VI_OPPEND_ESCAPE_BINDKEY=kj
ZVM_CURSOR_STYLE_ENABLED=false
ZVM_INIT_MODE=sourcing

# Which plugins would you like to load? (plugins can be found in ~/.oh-my-zsh/plugins/*)
# Custom plugins may be added to ~/.oh-my-zsh/custom/plugins/
# Example format: plugins=(rails git textmate ruby lighthouse)
# Add wisely, as too many plugins slow down shell startup.
plugins=(macos tmux postgres zsh-autosuggestions web-search zsh-vi-mode)

source $ZSH/oh-my-zsh.sh

# User configuration
# Hide user@hostname if it's expected default user
DEFAULT_USER="liqiang"
prompt_context(){}

# Setting rg as the default source for fzf
# export FZF_DEFAULT_COMMAND='rg --hidden --files'
# export FZF_DEFAULT_COMMAND="rg --files --follow --hidden --glob=\!.git"
export FZF_DEFAULT_COMMAND="rg --hidden --files -g '!{**/.git/*,**/node_modules/*,**/dist/*,**/bin/*}'"
# Apply the command to CTRL-T as well
export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"

# Set location of z installation
. /usr/local/etc/profile.d/z.sh

## FZF FUNCTIONS ##

# fo [FUZZY PATTERN] - Open the selected file with the default editor
#   - Bypass fuzzy finder if there's only one match (--select-1)
#   - Exit if there's no match (--exit-0)
fo() {
  local FILES
  IFS=$'\n' FILES=($(fzf-tmux -w 100% -h 100% --query="$1" --multi --select-1 --exit-0 --preview "bat --style=numbers --color=always --line-range :500 {}"))
  [[ -n "$FILES" ]] && ${EDITOR:-vim} "${FILES[@]}"
}

# fd - cd to selected directory
fd() {
  DIR=`find * -maxdepth 0 -type d -print 2> /dev/null | fzf-tmux` && cd "$DIR"
}
# fd() {
#   local dir
#   dir=$(find ${1:-.} -path '*/\.*' -prune -o -type d -print 2> /dev/null | fzf +m) && cd "$dir"
# }

fif() {
  if [ ! "$#" -gt 0 ]; then echo "Need a string to search for!"; return 1; fi
  local FILE
  FILE="$(rg --max-count=1 --ignore-case --files-with-matches --no-messages "$*" | fzf-tmux -w 100% -h 100% +m --preview="bat --style=numbers --color=always --line-range :500 {}")" && echo "opening $FILE" && vim "$FILE" || return 1;
}

# fda - including hidden directories
fda() {
  local DIR
  DIR=$(find ${1:-.} -type d 2> /dev/null | fzf +m) && cd "$DIR"
}

# fh [FUZZY PATTERN] - Search in command history
fh() {
  print -z $( ([ -n "$ZSH_NAME" ] && fc -l 1 || history) | fzf +s --tac | sed 's/ *[0-9]* *//')
}

# fbr [FUZZY PATTERN] - Checkout specified branch
# Include remote branches, sorted by most recent commit and limited to 30
fgb() {
  local branches branch
  branches=$(git for-each-ref --count=30 --sort=-committerdate refs/heads/ --format="%(refname:short)") &&
  branch=$(echo "$branches" |
           fzf-tmux -d $(( 2 + $(wc -l <<< "$branches") )) +m) &&
  git checkout $(echo "$branch" | sed "s/.* //" | sed "s#remotes/[^/]*/##")
}

# tm [SESSION_NAME | FUZZY PATTERN] - create new tmux session, or switch to existing one.
# Running `tm` will let you fuzzy-find a session mame
# Passing an argument to `ftm` will switch to that session if it exists or create it otherwise
ftm() {
  [[ -n "$TMUX" ]] && change="switch-client" || change="attach-session"
  if [ $1 ]; then
    tmux $change -t "$1" 2>/dev/null || (tmux new-session -d -s $1 && tmux $change -t "$1"); return
  fi
  session=$(tmux list-sessions -F "#{session_name}" 2>/dev/null | fzf --exit-0) &&  tmux $change -t "$session" || echo "No sessions found."
}

# tm [SESSION_NAME | FUZZY PATTERN] - delete tmux session
# Running `tm` will let you fuzzy-find a session mame to delete
# Passing an argument to `ftm` will delete that session if it exists
ftmk() {
  if [ $1 ]; then
    tmux kill-session -t "$1"; return
  fi
  session=$(tmux list-sessions -F "#{session_name}" 2>/dev/null | fzf --exit-0) &&  tmux kill-session -t "$session" || echo "No session found to delete."
}

# fkill - kill processes - list only the ones you can kill. Modified the earlier script.
fkill() {
    local pid
    if [ "$UID" != "0" ]; then
        pid=$(ps -f -u $UID | sed 1d | fzf -m | awk '{print $2}')
    else
        pid=$(ps -ef | sed 1d | fzf -m | awk '{print $2}')
    fi

    if [ "x$pid" != "x" ]
    then
        echo $pid | xargs kill -${1:-9}
    fi
}

fstash() {
  while out=$(git stash list "$@" |
              fzf --ansi --no-sort --reverse --print-query --query="$query"      \
                  --expect=ctrl-m,ctrl-b,ctrl-p,ctrl-d \
                  --preview-window=bottom:60% \
                  --preview 'git show --patience --stat --pretty=oneline --color=always \
                                  -p $(echo {} | cut -d: -f1 | less -R)' \
              );
  do
      # Tokenize selection by newline
      selection=("${(f)out}")

      # Keep the query accross fzf calls
      query="$selection[1]"

      # Represents the stash, e.g. stash{1}
      reflog_selector=$(echo "$selection[3]" | cut -d ':' -f 1)

      case "$selection[2]" in
          # enter or ctrl-m is just a diff
          ctrl-m)
              git diff --patience --color=always -p "$reflog_selector" | less -R
              ;;
          # ctrl-b checks out the stash as a branch and removes the stash
          ctrl-b)
              sha=$(echo "$selection[3]" | grep -o '[a-f0-9]\{7\}')
              git stash branch "stash-$sha" "$reflog_selector"
              break
              ;;
          #pop current stash into current branch
          ctrl-p)
              git stash pop "$reflog_selector"
              break
              ;;
          # del will drop the stash
          ctrl-d)
              git stash drop "$reflog_selector"
              ;;
      esac
  done
}

vf() {
  # note that you need to extract the absolute path in order to get this to work
  ls -d ~/.virtualenvs/*/ | fzf | while read file; do source $file/bin/activate; done
}

# Set default editor to nvim
export VISUAL=vim
export EDITOR=$VISUAL

# Enabled true color support for terminals
export NVIM_TUI_ENABLE_TRUE_COLOR=1

# zsh vi mode settings
VI_MODE_RESET_PROMPT_ON_MODE_CHANGE=true
VI_MODE_SET_CURSOR=true
MODE_INDICATOR="%F{yellow}+%f"

# web search blendopt setup
ZSH_WEB_SEARCH_ENGINES=(ghm "https://github.com/paradynsystems/blendopt/issues?q=is%3Aopen+assignee%3A%40")
alias ghm='web_search ghm me'

alias top='vtop --theme=wizard'
# alias ls='colorls --sd'
# alias ll='colorls -la --sd'
alias ll='ls -alhG'

alias grep='grep -i --color=auto --exclude-dir={.bzr,CVS,.git,.hg,.svn,.idea,.tox}'

alias we='curl wttr.in/Knoxfield\?3nqF'


[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

